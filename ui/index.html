<script type="text/javascript" src="static/js/jquery.js"></script>

<script type="text/javascript" src="static/js/spin.js"></script>
<script type="text/javascript" src="static/js/jquery.spin.js"></script>
<script type="text/javascript" src="static/js/jquery-deparam.js"></script>
<script type="text/javascript" src="static/js/underscore.js"></script>
<script type="text/javascript" src="static/js/coffee-script.js"></script>
<script type="text/javascript" src="static/js/bean.js"></script>

<script type="text/javascript" src="static/js/jquery.event.drag-2.2.js"></script>
<script type="text/javascript" src="static/js/jquery-ui.js"></script>
<script src="static/js/slick.core.js"></script>
<script src="static/js/slick.grid.js"></script>
<script src="static/js/slick.dataview.js"></script>
<script src="static/js/slick.groupitemmetadataprovider.js"></script>
<script src="static/js/slick.rowselectionmodel.js"></script>
<link rel="stylesheet" href="static/css/jquery-ui.css" type="text/css"/>
<link rel="stylesheet" href="static/css/slick.grid.css" type="text/css"/>
<link rel="stylesheet" href="static/css/slick.columnpicker.css" type="text/css"/>
<link rel="stylesheet" href="static/css/slick-default-theme.css" type="text/css"/>

<script type="text/javascript" src="static/js/polymaps.js"></script>
<script type="text/javascript" src="/static/js/d3.v3.js"></script>

<link rel="stylesheet" href="static/bootstrap-datepicker/datepicker.css" type="text/css"/>
<script type="text/javascript" src="static/js/moment.min.js"></script>
<script type="text/javascript" src="static/bootstrap-datepicker/bootstrap-datepicker.js"></script>

<link rel="stylesheet" href="static/bootstrap-3-timepicker/bootstrap-timepicker.min.css" type="text/css"/>
<script type="text/javascript" src="static/bootstrap-3-timepicker/bootstrap-timepicker.js"></script>

<script type="text/coffeescript" src="static/js/trusas.core.coffee"></script>
<script type="text/coffeescript" src="static/js/trusas.data.coffee"></script>

<script type="text/coffeescript" src="static/js/trusas.map.coffee"></script>

<script type="text/coffeescript" src="static/CREDITS.coffee"></script>
<script type="text/coffeescript" src="static/js/transit_analysis.coffee"></script>

<script type="text/javascript" src="static/js/buckets.js"></script>
<script type="text/coffeescript" src="static/js/dijkstra.coffee"></script>

<link rel="stylesheet" href="static/bootstrap/css/bootstrap.css" type="text/css"/>

<link rel="stylesheet" href="static/pnotify/jquery.pnotify.default.css" type="text/css"/>
<script type="text/javascript" src="static/pnotify/jquery.pnotify.js"></script>
<script type="text/coffeescript" src="static/js/global.errors.coffee"></script>

<link rel="stylesheet/less" href="static/css/transit_analysis.less" type="text/css"/>

<script type="text/javascript" src="static/js/less-1.4.1.js"></script>

<div id="global-loader" style="position: fixed; left: 0; top: 0; z-index: 100; width: 100%; height: 100%; background-color: rgba(0,0,0,0.7);"></div>
<script type="text/javascript">
	loader = $("#global-loader");
	loader.spin({color: "#FFF"});
</script>


<style type="text/css">
/* SVG with less seems to work a bit randomly, so let's hack here */
.route_select_map .point_layer {
	opacity: 0.7;
}

.route_select_map .nodepoint {
	fill: black;
	stroke-width: 2;
}

.route_select_map .nodepoint:hover {
	stroke: black;
	stroke-width: 9;
}

.route_select_map .selected_nodepoint {
	fill: red;
	stroke: red;
	stroke-width: 6;
}

.route_select_map .selected_nodepoint:hover {
	fill: red;
	stroke: red;
	stroke-width: 9;
}

.route_select_map {
	position: relative;
}

.route_select_map .map_controls {
	position: absolute;
	bottom: 0;
	padding: 10px;
	width: 100%;
}

.route-select-widgets {
	margin-top: 30px;
	margin-left: 20px;
	margin-right: 20px;
}
</style>

<script type="text/coffeescript">
route_link_template = _.template """
<a data-orighref="<%= url %>" class="view-route-link stateful-link">View</a>
"""

class Signal
	constructor: (@owner, @name) ->
	
	on: (cb) =>
		bean.on @owner, @name, cb
	off: (cb) =>
		bean.off @owner, @name, cb
	trigger: (args...) =>
		bean.fire @owner, @name, args

class SignalingValue
	constructor: (@_value) ->
		@changed = new Signal @, "changed"
	
	set: (@_value) =>
		@changed.trigger @_value
	
	get: => @_value
	
class Controller
	@Create: ->
		routes = $.getJSON '/resources/transit_routes.json'
		shapes = $.getJSON '/resources/coordinate_shapes.json'
		graph = $.getJSON '/resources/route_graph_edges.json'
		date_range = $.getJSON '/resources/available_date_range.json'
		promise = $.Deferred()
		$.when(routes, shapes, graph, date_range).done (args...) ->
			args = (a[0] for a in args)
			self = new Controller(args...)
			promise.resolve self

		return promise

	constructor: (@routes, @shapes, @graph, @date_range) ->
		@selected_nodes = new SignalingValue([])
		@selected_route = new SignalingValue()
		@reachable_nodes = new SignalingValue(@graph.nodes)
		@active_route = new SignalingValue(null)
	
	selection_url: =>
		return null if not @selected_route.get()
		return "route.html?route_nodes=" + @selected_nodes.get().join()


_setup_map = (ctrl) ->
	# Far from optimal
	graph = ctrl.graph
	
	coords = []
	ids = []

	for k, v of graph.nodes
		coords.push(v)
		ids.push(k)
	
	
	minc = [
		_.min(coords, (x) -> x[0])[0]
		_.min(coords, (x) -> x[1])[1]
		]
	
	maxc = [
		_.max(coords, (x) -> x[0])[0]
		_.max(coords, (x) -> x[1])[1]
		]
	
	selected_nodes = ctrl.selected_nodes
	
	Trusas.Map.Create($("#route_map")[0]).done (route_map) ->
		extent = [minc, maxc]
		route_map.set_extent(extent)
		styler = (style) -> style

		onload = (opts) ->
			for feat in opts.features
				hook_events(feat)
		
		points = []
		for i in [0...coords.length]
			c = coords[i]
			points.push
				lat: c[0]
				lon: c[1]
				id: ids[i]
		
		point_layer = route_map.add_points(points)
		
		d3.select(point_layer.element).selectAll('circle')
		.attr('class', 'nodepoint')
		.attr('r', 2)
		
		
		$(point_layer.element).find('.nodepoint').click (args...) ->
			$el = $ @
			sel = selected_nodes.get()
			id = $el.attr('id')
			idx = sel.indexOf(id)
			if idx >= 0
				sel.splice(idx, 1)
			else
				sel.push id
				
			selected_nodes.set(sel)

		# A weird mix of d3 and jquery deepens

		selected_nodes.changed.on (nodes) ->
			np = $ ".nodepoint"
			np.attr("class", "nodepoint")
			np.empty()
			for n in nodes
				el = $("##{n}")
				el.attr("class", "nodepoint selected_nodepoint")

		ctrl.selected_route.changed.on (route) ->
			route_map.remove_layer "selected_route"
			return if not route
			coords = (graph.nodes[k] for k in route)

			route_map.add_route coords, "selected_route",
				(style) -> style.attr("class", "selected_route_line")

		ctrl.reachable_nodes.changed.on (whitelist) ->
			$(".nodepoint").attr 'display', 'none'
			for node of whitelist
				$("##{node}").attr 'display', 'inline'
			for node in ctrl.selected_nodes.get()
				$("##{node}").attr 'display', 'inline'
		
		controls = $(route_map.el).append("""
		<div class="map_controls">
		<button class="clear_selection btn btn-danger">Clear selection</button>
		<a class="view_route btn btn-success pull-right stateful-link">View route</a>
		</div>
		""")
		clear_btn = controls.find('.clear_selection')
		clear_btn.addClass('disabled')
		selected_nodes.changed.on (sel) ->
			if sel.length > 0
				clear_btn.removeClass 'disabled'
			else
				clear_btn.addClass 'disabled'

		clear_btn.click ->
			selected_nodes.set([])
		
		view_btn = controls.find('.view_route')
		view_btn.addClass('disabled')
		ctrl.selected_route.changed.on (sel) ->
			if sel
				view_btn.removeClass 'disabled'
				view_btn.data "orighref", ctrl.selection_url()
				_update_link_state view_btn[0]
			else
				view_btn.addClass 'disabled'
				view_btn.removeAttr "href"

		ctrl.active_route.changed.on (route) ->
			route_map.remove_layer "active-shape"
			shape = null
			for i in [0...ctrl.shapes.length]
				if ctrl.shapes[i].shape == route
					shape = ctrl.shapes[i]
					break
			if not shape
				return

			route_map.add_route shape.coordinates, "active-shape"

_setup_table = (ctrl) ->
	data = ctrl.routes
	columns = ({id: n, field: n, name: n} \
		for n of data[0] when n not in ['id'])

	
	depcol = $.grep(columns, (e) -> e.id == 'departures')[0]
	depcol['groupTotalsFormatter'] = (total, coldef) ->
		return total.sum.departures
	
	routecol = $.grep(columns, (e) -> e.id == 'route_name')[0]
	routecol['formatter'] = (args...) ->
		row = args[4]
		urlargs = _.pick row, 'route_variant', 'direction', 'shape'

		url = "route.html?"+$.param urlargs
		return route_link_template url: url
		

	[columns[1], columns[0]] = [columns[0], columns[1]]
		
	groupItemMetadataProvider = new Slick.Data.GroupItemMetadataProvider()
	dataView = new Slick.Data.DataView
		groupItemMetadataProvider: groupItemMetadataProvider
		inlineFilters: true

	dataView.setGrouping(
		getter: 'route_name'
		formatter: (g) -> g.value
		#aggregators: [
		#	new Slick.Data.Aggregators.Sum("departures")
		#	]
		aggregateCollapsed: true
		collapsed: true)
	dataView.setItems(data)
	grid = new Slick.Grid "#departure_grid", dataView, columns,
		forceFitColumns: true
		multiSelect: false

	grid.registerPlugin groupItemMetadataProvider
	grid.setSelectionModel new Slick.RowSelectionModel()

	dataView.onRowCountChanged.subscribe (e, args) ->
		grid.updateRowCount()
		grid.render()
	
	dataView.onRowsChanged.subscribe (e, args) ->
		grid.invalidateRows(args.rows)
		grid.render()
	

	$('body').on "mouseenter", ".slick-row:not(.slick-group)", (ev) ->
		shape = $($(@).find('.slick-cell')[4]).html() # Hacky...
		ctrl.active_route.set shape
	
	$('body').on "mouseleave", ".slick-row:not(.slick-group)", (ev) ->
		ctrl.active_route.set null
	console.log "Done"

_setup_router = (ctrl) ->
	edges = {}
	for [from, to, data] in ctrl.graph.edges
		edges[from] ?= {}
		edges[from][to] = data
	
	ctrl.selected_nodes.changed.on (nodes) ->
		if nodes.length == 0
			ctrl.selected_route.set null
			ctrl.reachable_nodes.set ctrl.graph.nodes
			return
		
		reachable_shapes = {}

		# Find all shapes that have a route through
		# these nodes
		for shape in ctrl.shapes
			node_ids = shape.node_ids
			prev = node_ids.indexOf(nodes[0])
			continue if prev < 0
			idx = [prev]
			
			dobreak = false
			for n in nodes[1...]
				next = node_ids[prev+1...].indexOf(n)
				if next < 0
					dobreak = true
					break
				prev = next + prev + 1
				idx.push prev
			continue if dobreak
				
			reachable_shapes[shape.shape] =
				nodes: node_ids[idx[0]..idx[idx.length-1]]
				reachable_nodes: node_ids[idx[0]+1...]
				dist: shape.distances[idx[1]] - shape.distances[idx[0]]
		
		if nodes.length < 2
			reachable_nodes = {}
			for shape, data of reachable_shapes
				reachable_nodes[n] = true for n in data.reachable_nodes
			ctrl.selected_route.set null
			ctrl.reachable_nodes.set reachable_nodes
			return

		
		# Find shortest of such routes
		mindist = Number.MAX_VALUE
		minshape = undefined
		for shape, data of reachable_shapes
			if data.dist < mindist
				mindist = data.dist
				minshape = data

		# And filter only ones that are equal to this
		# A bit hacky comparison but should work
		minseqstr = "#{minshape.nodes}"
		for shape, data of reachable_shapes
			if "#{data.nodes}" != minseqstr
				delete reachable_shapes[shape]

		reachable_nodes = {}
		for shape, data of reachable_shapes
			reachable_nodes[n] = true for n in data.reachable_nodes

		ctrl.selected_route.set minshape.nodes
		ctrl.reachable_nodes.set reachable_nodes

# Yep. Almost copypaste from route.html
_setup_date_select = (ctrl) ->
	#times = (new Date(d.departure_time) for d in ctrl.departures)
	allweekdays = ['mon', 'tue', 'wed', 'thu', 'fri', 'sat', 'sun']
	console.log ctrl
	mindate = ctrl.date_range['mindate']
	maxdate = ctrl.date_range['maxdate']
	
	param = $.deparam(window.location.search[1...])
	start = new Date(param.start_date ? mindate)
	end = new Date(param.end_date ? maxdate)
	
	moment_format = "YYYY-MM-DD"
	# The picker has it's own formatter, but it seems
	# to be impossible to access due to the very annoying
	# jquery plugin interface thingy
	picker_format = "yyyy-mm-dd"

	starts = moment(start).format(moment_format)
	ends = moment(end).format(moment_format)
	element = $('#datepicker')
	startel = element.find("[name='start']").val(starts)
	endel = element.find("[name='end']").val(ends)

	cal = element.datepicker
		format: picker_format
		startDate: moment(mindate).format(moment_format)
		endDate: moment(maxdate).format(moment_format)
		autoclose: true
	
	if param.weekdays?
		sel_weekdays = param.weekdays.split(',')
	else
		sel_weekdays = allweekdays
	sel_weekdays.sort()
	
	weekdayel = $("#date-selector .weekdays")
	for weekday in sel_weekdays
		el = weekdayel.find("input[name=#{weekday}]")
		el.attr "checked", "checked"
	
	#$("#timepicker input").timepicker
	#	showMeridian: false
	#	template: "dropdown"

	starttime = param.start_time or "12:00"
	endtime = param.end_time or "12:00"

	start_time_el = $("#timepicker [name='starttime']")
		.val(starttime)
		.timepicker
			showMeridian: false
	
	end_time_el = $("#timepicker [name='endtime']")
		.val(endtime)
		.timepicker
			showMeridian: false
	
	update_date = ->
		
		
		opts = {}

		newweekdays = []
		for el in weekdayel.find('input')
			el = $ el
			if el.attr('checked')
				newweekdays.push el.attr "name"
		newweekdays.sort()
		
		if newweekdays.length == 0
			valid = false
		
		if newweekdays.length != 7
			opts.weekdays = newweekdays.join ','

		if "#{newweekdays}" != "#{sel_weekdays}"
			changed = true

		newstarttime = start_time_el.val()
		newendtime = end_time_el.val()

		if newstarttime != newendtime
			opts.start_time = newstarttime
			opts.end_time = newendtime

		newstartdate = startel.val()
		newenddate = endel.val()
		if newstartdate != starts or newenddate != ends
			opts.start_date = newstartdate
			opts.end_date = newenddate

		window.history.replaceState null, null, "?"+$.param(opts)
		_update_link_states()
	
	weekdayel.find('input').change update_date
	startel.change update_date
	endel.change update_date
	start_time_el.change update_date
	end_time_el.change update_date

_update_link_state = (link) ->
	console.log link
	$link = $ link
	orig = $link.data 'orighref'
	if typeof orig is 'undefined'
		return
	$link.attr("href", $link.data('orighref'))
	param = $.deparam $link[0].search[1...]
	state_param = $.deparam window.location.search[1...]
	$link[0].search = '?' + $.param _.extend(param, state_param)

# Hacking to ensure that "stateful links" carry
# the current "state" (ie. url get parameters)
_update_link_states = ->
	links = $ ".stateful-link"
	links.each (i, e) -> _update_link_state e

# A slow hack for handling dynamically inserted
# nodes
document.addEventListener 'DOMNodeInserted', (ev) ->
	links = $(ev.relatedNode).find ".stateful-link"
	links.each (i, e) -> _update_link_state e
	

$ ->
	Controller.Create().done (ctrl) ->
		_setup_date_select ctrl
		_setup_map ctrl
		_setup_table ctrl
		_setup_router ctrl
		_update_link_states()
		
		loader = $ "#global-loader"
		loader.spin(false)
		loader.remove()

</script>

<table class="header-container">
	<tr>
		<td id="session-info"></td>
		<td id="date-selector">
			<span class="input-timerange" id="timepicker">
				<span class="bootstrap-timepicker">
    				<input type="text" class="add-on input-xs" name="starttime" />
				</span>
    				<span>-</span>
				<span class="bootstrap-timepicker">
    				<input type="text" class="add-on input-xs" name="endtime" />
				</span>
			</span>

			<span class="input-daterange" id="datepicker">
    				<input type="text" class="input-xs" name="start" />
    				<span>-</span>
    				<input type="text" class="input-xs" name="end" />
			</span>

			<div class="btn-group weekdays">
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="mon" /> Mon
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="tue" /> Tue
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="wed" /> Wed
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="thu" /> Thu
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="fri" /> Fri
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="sat" /> Sat
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="sun" /> Sun
				</label>
			</div>
		</td>
	</tr>
</table>


<div class="widget-container route-select-widgets row">
	<div class="col-md-6" style="height: 425px">
		<axislabel class="toplabel acttopbin">Route map</axislabel>
		<div id="route_map" class="route_select_map"></div>
	</div>
	<div class="col-md-6" style="height: 425px;">
		<axislabel class="toplabel acttopbin">Route table</axislabel>
		<div id="departure_grid"></div>
	</div>
</div>
