<!DOCTYPE html>
<script type="text/javascript" src="static/js/jquery.js"></script>


<script type="text/javascript" src="static/js/spin.js"></script>
<script type="text/javascript" src="static/js/jquery.spin.js"></script>
<div id="global-loader" style="position: absolute; left: 0; top: 0; z-index: 100; width: 100%; height: 100%; background-color: rgba(0,0,0,0.7); margin: 0;"></div>
<script type="text/javascript">
	loader = $("#global-loader");
	loader.spin({color: "#FFF"});
</script>


<script type="text/javascript" src="static/js/bean.js"></script>
<script type="text/javascript" src="static/js/jquery-deparam.js"></script>
<script type="text/javascript" src="static/js/coffee-script.js"></script>


<link rel="stylesheet" href="static/bootstrap/css/bootstrap.css" type="text/css"/>
<script type="text/javascript" src="static/bootstrap/js/bootstrap.min.js"></script>

<link rel="stylesheet" href="static/pnotify/jquery.pnotify.default.css" type="text/css"/>
<script type="text/javascript" src="static/pnotify/jquery.pnotify.js"></script>
<script type="text/coffeescript" src="static/js/global.errors.coffee"></script>

<script type="text/coffeescript" src="static/js/trusas.core.coffee"></script>
<script type="text/coffeescript" src="static/js/trusas.data.coffee"></script>

<script type="text/coffeescript" src="static/CREDITS.coffee"></script>
<script type="text/coffeescript" src="static/js/transit_analysis.coffee"></script>

<!--<script type="text/javascript" src="static/js/Cesium/Cesium.js"></script>
<link rel="stylesheet" type="text/css" src="static/js/Cesium/Widgets/CesiumWidget/CesiumWidget.css" /> -->
<script type="text/javascript" src="static/js/polymaps.js"></script>

<script type="text/coffeescript" src="static/js/trusas.map.coffee"></script>

<script type="text/javascript" src="/static/js/jquery.flot.js"></script>
<script type="text/javascript" src="/static/js/jquery.flot.crosshair.js"></script>
<script type="text/javascript" src="/static/js/jquery.flot.navigate.js"></script>
<script type="text/javascript" src="/static/js/jquery.flot.selection.js"></script>
<script type="text/javascript" src="/static/js/jquery.flot.fillbetween.js"></script>

<script type="text/javascript" src="/static/js/d3.v3.js"></script>

<script type="text/javascript" src="/static/js/science.v1.js"></script>

<script type="text/javascript" src="static/js/underscore.js"></script>

<link rel="stylesheet" href="static/bootstrap-datepicker/datepicker.css" type="text/css"/>
<script type="text/javascript" src="static/js/moment.min.js"></script>
<script type="text/javascript" src="static/bootstrap-datepicker/bootstrap-datepicker.js"></script>

<link rel="stylesheet/less" href="static/css/transit_analysis.less" type="text/css"/>

<script type="text/javascript" src="static/js/less-1.4.1.js"></script>



<script type="text/coffeescript">

double_freedman_fiaconis_binw = (values) ->
	science.stats.iqr(values)/Math.pow(values.length, 1/3)
	

histogram = (values, binw=double_freedman_fiaconis_binw) ->
		min = Math.min values...
		max = Math.max values...
		values = _.sortBy values, (x) -> x
		step = binw values
		
		hist = [[min+step, 0]]
		currow = hist[0]
		for v in values
			while v > currow[0]
				currow = [currow[0]+step, 0]
				hist.push currow
			currow[1] += 1
		return [step, hist]

nonnans = (xs) -> (x for x in xs if x == x)

class DataController
	constructor: (@con) ->
		
		@cursor = new TrusasCursor()

		query = $.deparam(window.location.search[1..])
		traces_query = "resources/departure_traces.json"+window.location.search

		traces_promise = @con()
			.distance_grid()
			.time_spent_stats()
			.speed_stats()
			.departures()
			.stops()
			.stop_duration_stats()
			.inter_stop_duration_stats()
			.coordinate_shape()
			.date_range()()
		
		traces_promise.done @_setup_data
		traces_promise.done @_setup_cursor

		@span_durations = new TransAnal.DynamicValue (start, end) =>
			@con.span_durations start: start, end: end
		
		@ready = $.Deferred()
		
		promises = $.when traces_promise
		promises.done => @ready.resolve @
	
	_setup_cursor: (data) =>
		data = @route = @coordinate_shape
		
		# Remove edge-effects
		stats = @time_spent_stats.median
		n = stats.length
		for start in [0...n]
			s = stats[start]
			break if s == s

		for end in [n-2...0]
			s = stats[end]
			break if s == s
		
		mindist = @distance_grid[start]
		maxdist = @distance_grid[end]
		

		[lats, lons] = _.zip(data.coordinates...)
		@cursor.accommodateAxisRange mindist, maxdist
		@pos_to_coords = Trusas.coord_interp data.distances, lats, lons
		@cursorpath = Trusas.rangepath data.distances, lats, lons
		
		$(@cursor).on "activeRangeChange", dcall ((e, rng) =>
			@span_durations.refresh rng[0], rng[1]), 1000
	
	_setup_data: (data) =>
		for key of data
			@[key] = data[key]

	distanceGridIndex: (dist) =>
		return Math.round dist/@distance_bin_width

format_seconds = (seconds) ->
	minutes = Math.floor(seconds/60)
	seconds -= minutes*60
	seconds = "#{Math.round(seconds)}"
	
	if seconds.length < 2
		seconds = "0" + seconds
	return "#{minutes}:#{seconds}"

route_header_template = _.template """
	<a class="btn btn-default btn-xs" href="index.html">&lt;Back</a>
	<button class="btn btn-default btn-xs">
		Route <%= route_variant %>, direction <%= direction %>
		<span class="number-of-drives"></span>
	</button>
	"""

custom_header_template = _.template """
	<a class="btn btn-default btn-xs" href="index.html">&lt;Back</a>
	<button class="btn btn-default btn-xs">
		Custom route <span class="number-of-drives"></span>
	</button>
	"""



_setup = (ctrl) ->
	
	_setup_drive_stats ctrl
	_setup_date_select ctrl
	_setup_seq_plot ctrl
	_setup_map ctrl
	_setup_plots ctrl

	hash = window.location.hash[1..]
	if hash != ''
		rngÂ = (parseFloat p for p in hash.split ':')
		ctrl.cursor.setActiveRange rng
	else
		# Force-trigger cursor change so that the plots will load
		rng = ctrl.cursor.getActiveRange()
		ctrl.cursor.$.trigger "activeRangeChange", [rng]
	
	$(ctrl.cursor).on "activeRangeChange", (e, rng) ->
		hash = "#{Math.round(rng[0])}:#{Math.round(rng[1])}"
		window.location.replace(('' + window.location).split('#')[0] + '#' + hash)


_setup_map = (ctrl) ->
	Trusas.Map.Create($("#route-map")[0]).done (route_map) ->
		stats = ctrl.speed_stats
		end_distance = ctrl.distance_grid[ctrl.distance_grid.length-1]
		norm_distance = []
		for v in ctrl.distance_grid
			norm_distance.push v/end_distance

		#stat = (stats.lowq[i] - stats.highq[i] for i in [0...stats.highq.length])
		stat = stats.lowq
		#stat = (Math.log v + 1.0 for v in stat)
		validstat = (v for v in stat when v == v)
		min = Math.min validstat...
		max = Math.max validstat...
		rng = max - min
		norm_stat = []
		for v in stat
			norm_stat.push (v - min)/rng

		medinterp = Trusas.interp1d norm_distance, norm_stat
		route_map.add_colored_route ctrl.route.coordinates, medinterp,
			10.0/end_distance



		for stop in ctrl.stops
			route_map.add_marker [stop.latitude, stop.longitude]

		extent_setup = (rng) ->
			subpath = ctrl.cursorpath rng...
			extent = Trusas.coords_extent subpath
			route_map.set_extent extent
			route_map.set_active_path subpath
		
		ignore_next_range = false
		$(ctrl.cursor).on "activeRangeChange", (e, rng) ->
			# TODO: Don't calculate subpath twice
			subpath = ctrl.cursorpath rng...
			route_map.set_active_path subpath
			if ignore_next_range
				ignore_next_range = false
				return
				
			extent_setup rng

		extent_setup ctrl.cursor.getActiveRange()
		
		$(ctrl.cursor).on "hoverPositionChange", (e, pos) ->
			coords = ctrl.pos_to_coords pos
			if not coords[0]?
				coords = undefined
			route_map.set_hover coords
		
		route_map.onmove (ext) ->
			rng = Trusas.longest_clipped_linestring ext,
				ctrl.route.coordinates,
				ctrl.route.distances
			if rng[0] != rng[0]
				rng = ctrl.cursor.getAxisRange()
			# Ignore the next range change to avoid
			# free scrolling of the map
			ignore_next_range = true
			ctrl.cursor.setActiveRange rng

_setup_plots = (ctrl) ->
	stats = ctrl.speed_stats

	items = []

	color = "black"
	items.push
		data: _.zip(ctrl.distance_grid, stats.median)
		color: color
	
	items.push
		data: _.zip(ctrl.distance_grid, stats.lowp)
		id: 'lowp'
		shadowSize: 0
		lines: show: true, fill: false, lineWidth: 0
		color: color
	
	items.push
		data: _.zip(ctrl.distance_grid, stats.highp)
		shadowSize: 0
		fillBetween: 'lowp'
		lineWidth: 0
		color: color
		lines: show: true, fill: 0.3, lineWidth: 0
	
	items.push
		data: _.zip(ctrl.distance_grid, stats.lowq)
		id: 'lowq'
		shadowSize: 0
		color: color
		lines: show: true, fill: false, lineWidth: 0
	
	items.push
		data: _.zip(ctrl.distance_grid, stats.highq)
		shadowSize: 0
		fillBetween: 'lowq'
		lineWidth: 0
		color: color
		lines: show: true, fill: 0.3, lineWidth: 0
	
	timeticks = [0, 1, 5, 10, 20, 30, 60, 120, 300, 600]
	el = $("#route-speed")
	plot = $.plot el, items,
		zoom: interactive: true
		pan: interactive: true
		selection: mode: 'x'
		yaxis:
			panRange: false
			zoomRange: false
			#min: 0.0
			#ticks: timeticks
			#tickFormatter: format_seconds
			#max: 120
			#transform: (x) -> Math.log(x+1)
		xaxis:
			panRange: ctrl.cursor.getAxisRange()
			zoomRange: ctrl.cursor.getAxisRange()
			min: ctrl.cursor.getActiveRange()[0]
			max: ctrl.cursor.getActiveRange()[1]
		grid: hoverable: true
		crosshair: mode: 'x'
	
	el.on "plotselected", (ev, ranges) ->
		opts = plot.getAxes().xaxis.options
		opts.min = ranges.xaxis.from
		opts.max = ranges.xaxis.to
		plot.setupGrid()
		plot.draw()
		plot.clearSelection(false)
		plot.getPlaceholder().trigger("plotzoom", [ plot, [] ])

	update_cursor = ->
		axes = plot.getAxes()
		start = axes.xaxis.min
		end = axes.xaxis.max
		ctrl.cursor.setActiveRange([start, end])
	
	$(ctrl.cursor).on "activeRangeChange", ->
		rng = ctrl.cursor.getActiveRange()
		opts = plot.getAxes().xaxis.options
		opts.min = rng[0]
		opts.max = rng[1]
		plot.setupGrid()
		plot.draw()
	
	$(el).bind "plotzoom", update_cursor
	$(el).bind "plotpan", update_cursor

	$(el).bind "plothover", (event, pos, item) ->
		ctrl.cursor.setHoverPosition pos.x
	
	plot_durations = (durarr) ->
		durations = []
		for v in durarr
			continue if v != v
			durations.push v
		valid = deoutlier(durations)[0]
		[binsize, binned] = histogram(valid)
		n = valid.length
		for i in [0...binned.length]
			inbin = binned[i][1]
			binned[i][1] = inbin/n*100

		$.plot $("#route-duration"), [{
			data: binned,
			color: "black",
			}],
			bars:
				show: true
				barWidth: binsize
				lineWidth: 0
				fill: 0.5
			xaxis:
				tickFormatter: format_seconds
	
	ctrl.span_durations.$preupdate.on ->
		$("#route-duration").spin()

	ctrl.span_durations.$update.on (durarr) ->
		plot_durations durarr
		$("#route-duration").spin(false)

	plot_dur_tod = (durarr) ->
		durations = []
		durations = []
		for v in durarr
			continue if v != v
			durations.push v
		durarr = deoutlier(durations)[0]
		durations = []
		for i in [0...durarr.length]
			date = new Date ctrl.departures[i].departure_time
			minutes = date.getHours()*60 + date.getMinutes()
			durations.push [minutes, durarr[i]]

		$.plot $("#duration-by-tod"), [{data: durations, color: "rgb(0,0,0,0.5)"}],
			series:
				shadowSize: 0
				points:
					radius: 3
					show: true
					fill: 0.5
					fillColor: "black"
					lineWidth: 0
			xaxis:
				tickFormatter: (x) ->
					hours = Math.floor(x/60)
					minutes = String(x - hours*60)
					if minutes.length < 2
						minutes = "0" + minutes
					"#{hours}:#{minutes}"
			yaxis:
				tickFormatter: format_seconds
	
	ctrl.span_durations.$preupdate.on ->
		$("#duration-by-tod").spin()

	ctrl.span_durations.$update.on (data) ->
		plot_dur_tod data
		$("#duration-by-tod").spin(false)

_setup_seq_plot = (ctrl) ->
	plot = new TransAnal.StopSeqPlot "#route-seq", ctrl
	$(".acttopbin").click ->
		plot.pin_top_toggle()
	$(".actbottombin").click ->
		plot.pin_bottom_toggle()
		

_setup_date_select = (ctrl) ->
	#times = (new Date(d.departure_time) for d in ctrl.departures)
	allweekdays = ['mon', 'tue', 'wed', 'thu', 'fri', 'sat', 'sun']

	mindate = ctrl.date_range['mindate']
	maxdate = ctrl.date_range['maxdate']
	
	param = $.deparam(window.location.search[1...])
	start = new Date(param.start_date ? mindate)
	end = new Date(param.end_date ? maxdate)
	
	moment_format = "YYYY-MM-DD"
	# The picker has it's own formatter, but it seems
	# to be impossible to access due to the very annoying
	# jquery plugin interface thingy
	picker_format = "yyyy-mm-dd"

	starts = moment(start).format(moment_format)
	ends = moment(end).format(moment_format)
	element = $('#datepicker')
	startel = element.find("[name='start']").val(starts)
	endel = element.find("[name='end']").val(ends)
	filterbutton = $ "#date-selector #filter-dates"

	cal = element.datepicker
		format: picker_format
		startDate: moment(mindate).format(moment_format)
		endDate: moment(maxdate).format(moment_format)
		autoclose: true
	
	if param.weekdays?
		sel_weekdays = param.weekdays.split(',')
	else
		sel_weekdays = allweekdays
	sel_weekdays.sort()
	
	weekdayel = $("#date-selector .weekdays")
	for weekday in sel_weekdays
		el = weekdayel.find("input[name=#{weekday}]")
		el.attr "checked", "checked"

	update_date = ->
		newstart = startel.val()
		newend = endel.val()
		changed = false
		valid = true
		changed = true if newstart != starts
		changed = true if newend != ends

		newweekdays = []
		for el in weekdayel.find('input')
			el = $ el
			if el.attr('checked')
				newweekdays.push el.attr "name"
		newweekdays.sort()
		
		if newweekdays.length == 0
			valid = false

		if "#{newweekdays}" != "#{sel_weekdays}"
			changed = true

		activate_btn = changed and valid
		filterbutton.toggleClass "disabled", not activate_btn
		filterbutton.toggleClass "btn-default", not activate_btn
		filterbutton.toggleClass "btn-success", activate_btn
		
		filterbutton[0].href = window.location
		param = $.deparam(filterbutton[0].search[1...])

		
		opts =
			start_date: newstart
			end_date: newend
			weekdays: newweekdays.join(',')

		for k, v of opts
			param[k] = v
		
		filterbutton[0].search = "?"+$.param(param)
	
	weekdayel.find('input').change update_date
	startel.change update_date
	endel.change update_date

_setup_drive_stats = (ctrl) ->
	departures = ctrl.departures
	
	$("#session-info .number-of-drives")
		.html "(#{departures.length} drives)"
	
	table = $ """
	<table class="departure-statistics">
	<tr>
		<th>Route</th>
		<th>Variant</th>
		<th>Direction</th>
		<th>Shape</th>
		<th>Drives</th>
	</tr>
	</table>
	"""

	row = _.template """
	<tr>
		<td><%= name %></td>
		<td><%= variant %></td>
		<td><%= direction %></td>
		<td><%= shape %></td>
		<td><%= ndrives %></td>
	</tr>
	"""

	stats = {}
	for d in departures
		grp =
			name: d.route_name
			variant: d.route_variant
			direction: d.direction
			shape: d.shape
		
		grp = JSON.stringify(grp)
		if grp of stats
			stats[grp] += 1
		else
			stats[grp] = 1
	
	statlist = []
	for k, v of stats
		grp = JSON.parse k
		grp.ndrives = v
		statlist.push grp
	
	statlist = _.sortBy statlist, (d) -> -d.ndrives

	table.append $ row grp for grp in statlist
	
	po = $("#session-info > button").popover
		html: true
		content: table
		placement: "bottom"
		container: "body"
	
$ ->
	query = $.deparam(window.location.search[1..])
	if "route_nodes" of query
		header = custom_header_template query
	else
		header = route_header_template query
	
	$("#session-info").html(header)
	
	promise = TransAnal.Stats.Connect query
	promise.done (con) ->
		ctrl = new DataController(con)
		ctrl.ready.done _setup
		ctrl.ready.done ->
			loader = $ "#global-loader"
			loader.spin(false)
			loader.remove()

</script>

<!-- Yes, it's a table for layouting. It works. Deal with it. -->
<table class="header-container">
	<tr>
		<td id="session-info"></td>
		<td id="date-selector">
			<span class="input-daterange" id="datepicker">
    				<input type="text" class="input-xs" name="start" />
    				<span>-</span>
    				<input type="text" class="input-xs" name="end" />
			</span>

			<div class="btn-group weekdays">
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="mon" /> Mon
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="tue" /> Tue
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="wed" /> Wed
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="thu" /> Thu
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="fri" /> Fri
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="sat" /> Sat
				</label>
				<label class="btn btn-default btn-xs">
					<input type="checkbox" name="sun" /> Sun
				</label>
			</div>

			<a id="filter-dates" class="btn btn-xs disabled btn-default">Filter</a>
		</td>
	</tr>
</table>

<div class="widget-container" class="row">
	<div class="col-xs-6" style="height: 425px">
		<div id="route-map"></div>
	</div>
	<div class="col-xs-6" style="height: 200px;">
		<div id="route-speed"></div>
		<axislabel class="xlabel">Distance (m)</axislabel>
		<axislabel class="ylabel">Speed (km/h)</axislabel>
	</div>
	<div class="col-xs-6" style="height: 200px;">
		<div id="route-duration"></div>
		<axislabel class="xlabel">Time spent (m:s)</axislabel>
		<axislabel class="ylabel">Share of drives (%)</axislabel>

	</div>
	<div class="col-xs-6" style="height: 200px;">
		<axislabel class="toplabel acttopbin">Stops (stop %)</axislabel>
		<div id="route-seq"></div>
		<axislabel class="xlabel actbottombin">Between stops (time spent)</axislabel>
	</div>

	<div class="col-xs-6" style="height: 200px;">
		<div id="duration-by-tod"></div>
		<axislabel class="xlabel">Time of day</axislabel>
		<axislabel class="ylabel">Time spent (m:s)</axislabel>
	</div>
</div>
